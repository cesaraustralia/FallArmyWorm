---
title : GrowthMaps.jl example
author : Rafael Schouten, James Maino
---

# Growth rate calculation


In this example we will calculate the expected population growth rates of our species.

Use biological data from Early 2018
GBIF distribution data
Spread data from Early 2018

## Load some required packages

These packages take care of loading and plotting data, and handling sci units and dates.

```julia; results=hidden
using GrowthMaps, Plots, Unitful, UnitfulRecipes, Dates, Setfield, Statistics
using GeoData, ArchGDAL, NCDatasets
using Unitful: °C, K, cal, mol
basedir = @__DIR__
```


## Define model components

First we'll define the growth model using `SchoolfieldIntrinsicGrowth`, based on
Schoolfield (1981).

When defining model components, the first parameter is a `:symbol` for the
required raster layer in the source data.

```julia; results=hidden
p = 0.17
ΔH_A = 15588.0cal/mol
ΔH_H = 73744.0cal/mol
Thalf_H = 307.6K
ΔH_L = 73744.0cal/mol
Thalf_L = 500.0K
T_ref = K(27.0°C)
growthmodel = SchoolfieldIntrinsicGrowth(p, ΔH_A, ΔH_L, Thalf_L, ΔH_H, Thalf_H, T_ref)
growth = Layer(:surface_temp, K, growthmodel)
```

If these are only estimated parameters, We can fit the model to a a dataset of growth
rate and temperature. First extract our independent and dependent variables from the example CSV:

```julia; results=hidden
# using CSV, DataFrames, Tables
# mkpath(joinpath(basedir, "build"))
# obsdata = CSV.File(joinpath(basedir, "swd_ecophys_data.csv"), select=[:x_value, :y_value, :y_key]) |> DataFrame
# obsdata = filter(d -> d.y_key == "r_m", obsdata) |> dropmissing
```


Then extract the required data colummns, and convert temperature values from
unitless Celcius to explit Kelvins, using Unitful.jl:

```julia; results=hidden
# obsrate = obsdata.y_value
# obstemp = obsdata.x_value * °C .|> K
# obs = collect(zip(obstemp, obsrate))
```

Now plot the growth rate curve:

```julia; results=hidden
# TODO how to pass vector of values to rate
temprange = K.( collect(1.0:40.0)*°C )
p = plot(x -> GrowthMaps.rate(growth, x), temprange; label="growth rate")
# scatter!(p, obs; label="observed ")
```

We can try tweaking the fitting the model manually in a user interface.
Model components are immutable (for performance reasons), so we wrap the model
in a mutable wraper so we can use the results.
We parametrise the model over the same temperature range that we are plotting,
using the :surface_temp key that the model requires:

```julia; results=hidden
wrapper = ModelWrapper(growth)
tempdata=(surface_temp=temprange,)
manualfit!(wrapper, tempdata)
```


If you are happy with the result, you we can update extract the manual fit
to use to generate our growth rate maps:

```julia; results=hidden
# fittedgrowth = wrapper.model
# growth = growth
```

Note that `manualfit!` will also work for a tuple of model components
that use the same source data, like `(growth, heatstress, coldstress)`.


## Load spatial data

Using the monthly aggregated SMAP dataset

```julia; results=hidden
using HDF5
# smap_folder = "/home/raf/Data/SMAP/SMAP_L4_SM_gph_v4"
smap_folder = "F:/SMAP/SMAP_monthly_midpoint_subset"
tstart, tstop = DateTime(2017), DateTime(2018)
days = (15,)
series = SMAPseries(smap_folder)[Where(t -> dayofmonth(t) in days)]
```

Otherwise get the series from tiff files, which is a little more hands-on:

```julia; results=hidden
# unzippedfolder = "/home/raf/Work/cesar/Raster/SMAP/SMAP_aggregated27km"
# filenames = readdir(unzippedfolder)
# wilting_paths =
#     joinpath.(Ref(unzippedfolder), filter(fn -> occursin(r"land_fraction_wilting", fn), filenames))
# surface_temp_paths =
#     joinpath.(Ref(unzippedfolder), filter(fn -> occursin(r"surface_temp", fn), filenames))
#
# # Get the dates covered in the data from the `surface_temp` files list, using regex:
# df = DateFormat("yyyymmddTHHMMSS")
# dates = DateTime.(replace.(surface_temp_paths, Ref(r".*_(\d+T\d+).tif" => s"\1")), Ref(df))
#
# #= We know the "land_fraction_wilting" files are for the same dates.
# Now we have the files and date seies, we can put together
# a series of GeoData.jl stacks to load from disk while generating growth maps
# The beauty of this approach is that we can use a lot of different source file types and
# folder configurations without converting them or running out of RAM.
# =#
# makestack(i) =
#  GDALstack((land_fraction_wilting=wilting_paths[i], surface_temp=surface_temp_paths[i]);
#            window=(Band(1),), usercrs=EPSG(4326))
# timedim = Ti(dates; mode=Sampled(Ordered(), Regular(Hour(3)), Intervals(Start())))
# stacks = [makestack(i) for i in 1:length(surface_temp_paths)]
# series = GeoSeries(stacks, (timedim,))
```

We can plot a layer from a file at some date in the series:

```julia; results=hidden
series[2][:surface_temp] |> plot
```

First we'll run this basic growth model:

```julia; results=hidden
growthrates = mapgrowth(growth, series=series,
    period=Year(1),
    nperiods=1,
    startdate=first(bounds(series, Ti))
)
# growthrates[Ti(1:3:12)] |> plot # doesnt work
growthrates[Ti(1)] |> plot

```

Now define some stressors:

```julia; results=hidden
coldthresh = 10.0°C |> K
coldmort = -0.025 * K^-1
coldstress = Layer(:surface_temp, K, ColdStress(coldthresh, coldmort))

heatthresh = 40.0°C |> K
heatmort = -0.12 * K^-1
heatstress = Layer(:surface_temp, K, HeatStress(heatthresh, heatmort))

wiltthresh = 0.1
wiltmort = -0.2
wiltstress = Layer(:land_fraction_wilting, WiltStress(wiltthresh, wiltmort));

```


To build a more complex model, we can chain components together in a tuple,
and run them:

```julia; results=hidden
growthrates = mapgrowth((growth, heatstress, coldstress, wiltstress);
    series=series,
    period=Month(1),
    nperiods=12,
    startdate=first(bounds(series, Ti))
)

plot(growthrates[Ti(1)], clims=(0.0, 0.15), axis=false)
```

## Compare with observation data

To compare out simulation with observations data, we'll load them
from a CSV file:

```julia; results=hiddeats
using CSV
csvfilename = "data/compiled_occurence_records.csv"
obs = CSV.File(csvfilename)
occurrence = collect(zip(obs.X, obs.Y))
```

And scatter them on the growthrates map:
```julia; results=hiddeats
p = growthrates[Ti(1)] |> plot
scatter!(p, occurrence; markersize=4.0, markercolor=:green, markershape=:circle, label="obs")
```

# Parametrising models using interactive maps

If you need to adjust the model based on the distribution, this
can be done live in the interface, as with the manual fit.

Parametrising maps on large datasets is processor intensive, which inhibits
interactive fedback. To reduce processing, we can aggregate the spatial data to
a more manageable size.

You can experiment with the `agg` size to compromise between quality and render time. Large values
will look pixelated but will run fast.

```julia; results=hidden
agg = 10
aggseries = GeoData.aggregate(Center(), series, (Lon(agg), Lat(agg)), (:land_fraction_wilting, :surface_temp))
p = first(aggseries[1]) |> plot
scatter!(p, occurrence; markersize=4.0, markercolor=:green, markershape=:circle, label="obs")
```

Fit a model across the whole year to see where population growth is on average positive across the year.

```julia; results=hidden
coldthresh = 13.0°C |> K
coldmort = -0.5 * K^-1
coldstress = Layer(:surface_temp, K, ColdStress(coldthresh, coldmort))

growthrates = mapgrowth((growth, coldstress, wiltstress);
    series=aggseries,
    period=Month(1),
    nperiods=12,
    startdate=first(bounds(series, Ti))
) |> x->permutedims(x, (Lat, Lon, Ti))
p = plot(growthrates[Ti([1,2,3,4]), usa...], clims=(0.0, 0.15), axis=false)
# scatter!(p, occurrence; markersize=1.0, markeralpha=1, markerstrokecolor = :white, markershape=:circle, label="obs")
```

```julia; results=hidden
growthrates = mapgrowth((growth, coldstress, heatstress, wiltstress);
    series=aggseries,
    period=Year(1),
    nperiods=1,
    startdate=first(bounds(series, Ti))
) |> x->permutedims(x, (Lat, Lon, Ti))
p = plot(growthrates[Ti(1),aust...], clims=(0.0, 0.15), axis=false)
```

As the combination of model is additive, we can prebuild parts of the model
we don't want to fit manually, which simplifies the interfaces and helps performance.
Seeing we allready fit the growth response to empiracle data, lets just fit
the stress responses to the map:

```julia; results=hidden
modelkwargs = (
    series=aggseries,
    period=Month(12),
    nperiods=1,
    startdate=first(bounds(series, Ti))
)
precomputed = mapgrowth(growth; modelkwargs...)
```

Then fit the other components. `throttle` will dictate how fast the interface updates. Make it larger on a slow machine,
smaller on a faster one.

```julia; results=hidden
wrapper = ModelWrapper(wiltstress, coldstress, heatstress)
throttle = 0.2
mapfit!(wrapper, modelkwargs;
    window=(Ti(1),),
    occurrence=occurrence,
    precomputed=precomputed,
    throttle=throttle,
    markershape=:cross,
    markercolor=:lightblue,
    markeropacity=0.4
)
```

And get the updated model components from the wrapper:

```julia; results=hidden
wiltstress, coldstress, heatstress = wrapper.model
```

Now we will put together decent population growth maps
using the higher resolution data, with a monthly timestep:

```julia; results=hidden
growthrates = mapgrowth((growth, wiltstress, coldstress, heatstress),
    series=series,
    period=Month(1),
    nperiods=12,
    startdate=first(bounds(series, Ti))
);
# plot(growthrates[Ti(1:3:12)]; clims=(0.0, 0.15), axis=false)
```

This is ready to use for projecting growthrates, or in a dispersal
simulation using Dispersal.jl. Lets save it as a NetCDF file:

```julia; results=hidden
write("data/growthrates.ncd", NCDarray, growthrates)
```

![Growth Rates](../assets/growthrates.png)

We can load the data again with:

```julia; results=hidden
# growthrates = NCDarray("data/growthrates.ncd")

growthrates = NCDarray(growthratesfilepath) |>
    x->setdims(x, (@set dims(x, Ti).mode.span = Regular(Month(1)))) |>
    x->permutedims(x, (Lat, Lon, Ti)) |>
    x->reverse(x; dims=Lat)

plot(growthrates[Ti(1:3:12)]; clims=(0.0, 0.11), axis=false, legend=false, title="")
savefig("output/population_growth_quarters.png")

growthrates |>
  x -> mean(x, dims=Ti) |>
  x -> plot(x; clims=(0.0, 0.11), axis=false, legend=false, title="")

meangrowth = @set mean(growthrates, dims=Ti).name = "Population growth rate potential (1/d)"
p = plot(meangrowth;
    clims=(0.0, 0.15),
    title="Fall Armyworm mean growth rate"
)
scatter!(p, occurrence;
    markersize=0.3,
    markeralpha=0.7,
    markerstrokecolor=:lightgreen,
    markercolor=:green,
    markershape=:circle,
    label="observations"
)
savefig("output/population_growth_mean.png")
```

Now, if you need to use the model for a serious application, run it on the real
SMAP dataset. GeoData.jl has a `SMAPseries()` constructor that will automate the
whole process of loading real SMAP HDF5 files.

```julia
using HDF5
# Add the path to your SMAP data folder
tstart, tstop = DateTime(2017), DateTime(2018)
days = (1, 4, 8, 12, 16, 20, 23, 27)
smapseries = SMAPseries(smap_folder)[Where(t -> dayofmonth(t) in days)]
@time output = mapgrowth(
    (growth, wiltstress, coldstress, heatstress);
    series=smapseries,
    period=Month(1),
    nperiods=12,
    startdate=tstart,
);

plot(output[Ti(1:3:12)]; axis=false)
plot(mean(output, dims=Ti), clims=(0.0, 0.15))

growthratesfilepath = joinpath(basedir, "data", growthratesfilename)
write(growthratesfilepath, NCDarray, output)
```
